plugins {
    id "java"
    id "org.beryx.runtime" version "1.8.1"
}

sourceCompatibility = 1.8
sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["resources/"]

project.ext.mainClassName = "com.galvarez.ttw.desktop.DesktopLauncher"

task debug(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

dist.dependsOn classes

application {
    mainClassName = project.mainClassName
    applicationName = 'TheShapeOfThingsThatWere'
    applicationDefaultJvmArgs = [ ]
}

runtime {
    options = [
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages',
            '--add-modules=jdk.unsupported' // required by lwjgl for sun.misc.Unsafe
    ]
    imageZip = file("$buildDir/TheShapeOfThingsThatWere.zip")
}

tasks.runtime.doLast {
    copy {
        from('resources')
        into("$buildDir/image/bin")
    }
    new File("$buildDir/image/", "start.bat").text = """
cd bin
TheShapeOfThingsThatWere.bat
"""
}
